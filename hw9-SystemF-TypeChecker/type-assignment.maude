---in lambda-types.maude .
---in builtins.maude .

mod TYPE-ASSIGNMENT is including LAMBDA-TYPES + PL-ID .
  sort TypeAssignment .
  subsort Undefined < Type .

  op _|->_ : List{Id} Type -> TypeAssignment [prec 0] .
  op emptyTypeAssignment : -> TypeAssignment .
  op _&_ : TypeAssignment TypeAssignment -> TypeAssignment [assoc comm id: emptyTypeAssignment format(d s s d)] .

  op _(_) : TypeAssignment Id -> Type [prec 0] .          --- lookup
  op _[_/_] : TypeAssignment Type Id -> TypeAssignment [prec 0] .  --- update

  var Sigma : TypeAssignment .  var T T' : Type .  var X X' : Id .  var Xl : List{Id} .

  eq X |-> undefined = emptyTypeAssignment .                 --- "undefine" a variable in a TypeAssignment

  eq (Sigma & X |-> T)(X) = T .
  eq Sigma(X) = undefined [owise] .

  eq (Sigma &  X |-> T)[T' / X ] = (Sigma & X |-> T') .
  eq Sigma[T / X] = (Sigma & X |-> T) [owise] .

  eq (X,X',Xl) |-> T = X |-> T & X' |-> T & Xl |-> T .
  eq .List{Id} |-> T = emptyTypeAssignment .
endm

rew (x |-> nat & y |-> bool )(y) .
rew (x |-> nat & y |-> bool )(x) .